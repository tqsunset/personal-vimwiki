### 오늘 한 프로젝트

- twitter bot 개인 프로젝트



### 오늘의 워크 플로우

- crc-trigger 함수 작동 확인 및 수정

  - crc 트리거 함수가 정상작동하지 않고 could not authenticate you 메세지를 자꾸 반환받음. 당시 인증 관련 코드.

  ```clojure
  (def oauth-params {:oauth_consumer_key     app-consumer-key
                     :oauth_nonce            (rand-str 30)
                     :oauth_signature_method "HMAC-SHA1"
                     :oauth_timestamp        (str (int (/ (System/currentTimeMillis) 1000)))
                     :oauth_token            user-access-token
                     :oauth_version          "1.0"})
  
  (defn param-str [param]
    (->> param
         (apply conj oauth-params)
         (mapv (fn [[key value]]
                 (str (url-encode (name key)) "=" (url-encode value))))
         (string/join "&")))
  
  (defn sig-string [type url params]
    (let [method (clojure.string/upper-case (name type))
          encoded-url (url-encode url)
          encoded-param-str (url-encode (param-str params))]
      (string/join "&" [method encoded-url encoded-param-str])))
  
  (def sig-key
    (let [encoded-consumer-secret (url-encode app-consumer-secret)
          encoded-token-secret (url-encode user-access-token-secret)]
      (str encoded-consumer-secret "&" encoded-token-secret))) ;signature base string
  
  (defn signature [type url params]
    (hmac-sha1 (sig-string type url params) sig-key))
  
  (defn auth-header
    [type url params]
    (->> {:oauth_signature (url-encode (signature type url params))}
         (apply conj oauth-params)
         (sort-by last)
         (mapv (fn [[key value]] (str (name key) "=\"" value "\"")))
         (string/join ",")
         (str "OAuth ")))
  ```

  - `(def oauth-param {...})`와 같이 변수 선언했기 때문에 타임스탬프와 nonce값이 선언 당시의 값(코드 로딩 시의 값)으로 고정되어버림. 두 값은 리퀘 마다 고유의 값이어야하므로 수정 필요.    
  - `(defn oauth-param [ ] {...})`와 같이 해당 변수를 함수화하면 함수 평가?시 마다 새로운 값이 계산 된다. 하지만 이 함수를 `param-str`과 `sig-string`내에서 각자 따로 호출하게 되므로 하나의 oauth-param에 대한 시그니쳐 생성이 불가능해짐. 따라서 다음처럼 하나의 리퀘 안에서는 함수가 모두 동일한 지역변수 oauth-param의 값을 이용하도록 수정. auth-header에서 let문으로 지역변수를 설정해주고 시그니처 생성을 담당하는 함수들에게 모두 인자로 넘겨줌.    

  ```clojure
  (defn param-str [param oauth-params]
    (->> param
         (apply conj oauth-params)
         (mapv (fn [[key value]]
                 (str (url-encode (name key)) "=" (url-encode value))))
         (string/join "&")))
  
  (defn sig-string [type url params oauth-params]
    (let [method (clojure.string/upper-case (name type))
          encoded-url (url-encode url)
          encoded-param-str (url-encode (param-str params oauth-params))]
      (string/join "&" [method encoded-url encoded-param-str])))
  
  (def sig-key
    (let [encoded-consumer-secret (url-encode app-consumer-secret)
          encoded-token-secret (url-encode user-access-token-secret)]
      (str encoded-consumer-secret "&" encoded-token-secret))) ;signature base string
  
  (defn signature [type url params oauth-params]
    (hmac-sha1 (sig-string type url params oauth-params) sig-key))
  
  (defn auth-header
    [type url params]
    (let [oauth-params {:oauth_consumer_key     app-consumer-key
                        :oauth_nonce            (rand-str 30)
                        :oauth_signature_method "HMAC-SHA1"
                        :oauth_timestamp        (str (int (/ (System/currentTimeMillis) 1000)))
                        :oauth_token            user-access-token
                        :oauth_version          "1.0"}]
      (->> {:oauth_signature (url-encode (signature type url params oauth-params))}
          (apply conj oauth-params)
          (sort-by last)
          (mapv (fn [[key value]] (str (name key) "=\"" value "\"")))
          (string/join ",")
          (str "OAuth "))))
  ```

  - 다시 crc-trigger 함수 테스트 하자 204 정상 응답을 받을 수 있었다. 

  - 인증 관련 함수들이 oauth-params을 좀 덜 번잡하게 참조할 수 있게 작성 가능할 것도 같은데 함 보아야.

  

### 오늘 프로젝트에서 배운 것

- 



### 프로젝트와 관련 없지만 알게 된 것

- 



### 내일 할 것

- 



